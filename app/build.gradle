apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId "com.ape.material.weather"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode VERSION_CODE.toInteger()
        versionName VERSION_NAME
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "HEWEATHER_KEY", HEWEATHER_KEY
        buildConfigField "String", "HEWEATHER_URL", HEWEATHER_FREE_URL
    }
    def filePrivateProperties = file("../public.properties")
    if (filePrivateProperties.exists()) {
        signingConfigs {
            debug {
                Properties propsPrivate = new Properties()
                propsPrivate.load(new FileInputStream(filePrivateProperties))

                storeFile file(propsPrivate['key.store'])
                keyAlias propsPrivate['key.alias']
                storePassword propsPrivate['key.store.password']
                keyPassword propsPrivate['key.alias.password']
            }
        }
    }

    applicationVariants.all { variant ->//获取 variant 参数，就是 productFlavor x buildType
        variant.outputs.each { output ->//获取输出文件
            def file = output.outputFile//修改实例
            if (file != null && file.name.endsWith('-release.apk')) {//变种名以release.apk结尾时才修改名字
                def fileName = file.name.replace(file.name, "MaterialWeather" + "_" + variant.versionName + ".apk")
                output.outputFile = new File(file.parentFile, fileName)
            } else if (file != null && file.name.endsWith('-debug.apk')) {
                def fileName = file.name.replace(file.name, "MaterialWeather-debug" + "_" + variant.versionName + ".apk")
                output.outputFile = new File(file.parentFile, fileName)
            }
        }
    }

    buildTypes {
        debug {
            // 显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"
            //是否开启ACRA
            buildConfigField "boolean", "ACRA_ENABLED", "false"
            manifestPlaceholders = [amap_key: AMAP_KEY]
        }
        release {
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"
            //是否开启ACRA
            buildConfigField "boolean", "ACRA_ENABLED", "true"
            manifestPlaceholders = [amap_key: AMAP_KEY]
            if (filePrivateProperties.exists()) {
                signingConfig signingConfigs.debug
            }
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

    }

    lintOptions {
        abortOnError
        disable "ResourceType"
        disable 'MissingTranslation'
        disable 'ExtraTranslation'
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    compile "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:design:$rootProject.supportLibraryVersion"
    //butterknife
    compile "com.jakewharton:butterknife:$rootProject.butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.butterknifeVersion"

    //permission dispatcher
    compile 'com.github.hotchemi:permissionsdispatcher:2.4.0', {
        // if you don't use android.app.Fragment you can exclude support for them
        exclude module: "support-v13"
    }
    annotationProcessor 'com.github.hotchemi:permissionsdispatcher-processor:2.4.0'

    //rxjava
    compile 'io.reactivex.rxjava2:rxjava:2.1.3'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'

    //rxlife
    compile 'com.trello.rxlifecycle2:rxlifecycle:2.1.0'
    compile 'com.trello.rxlifecycle2:rxlifecycle-android:2.1.0'
    compile 'com.trello.rxlifecycle2:rxlifecycle-components:2.1.0'

    //retrofit
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'

    compile('com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:0.10.6@aar') {
        transitive = true
    }

    //loading layout
    compile 'com.lai.weavey:loadinglayout:1.3.1'

    // Dagger dependencies
    compile "com.google.dagger:dagger:$rootProject.daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$rootProject.daggerVersion"

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'

    debugCompile 'io.github.skyhacker2:sqliteonweb:1.0.2'
    releaseCompile 'io.github.skyhacker2:sqliteonweb-no-op:1.0.2'

    //高德定位
    compile 'com.amap.api:location:latest.integration'

    //rxcache
    compile "com.github.VictorAlbertos.RxCache:runtime:1.8.1-2.x"
    compile 'com.github.VictorAlbertos.Jolyglot:gson:0.0.4'

    compile 'com.github.LinweiJ:ViewPagerIndicator:0.0.2'
}
